# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
# This config sets the following variables in your project::
#
#   Iceberg_FOUND - true if Iceberg found on the system
#   Iceberg_VERSION - version of the found Iceberg
#
# This config sets the following targets (if built) in your project::
#
#   iceberg::iceberg_shared
#   iceberg::iceberg_static
#   iceberg::iceberg_bundle_shared
#   iceberg::iceberg_bundle_static
#   iceberg::iceberg_rest_shared
#   iceberg::iceberg_rest_static

@PACKAGE_INIT@

set(ICEBERG_BUILD_STATIC "@ICEBERG_BUILD_STATIC@")
set(ICEBERG_SYSTEM_DEPENDENCIES "@ICEBERG_SYSTEM_DEPENDENCIES@")

include(CMakeFindDependencyMacro)

macro(iceberg_find_dependencies dependencies)
  if(DEFINED CMAKE_MODULE_PATH)
    set(ICEBERG_CMAKE_MODULE_PATH_OLD ${CMAKE_MODULE_PATH})
  else()
    unset(ICEBERG_CMAKE_MODULE_PATH_OLD)
  endif()
  set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

  foreach(dependency ${dependencies})
    find_dependency(${dependency})
  endforeach()

  if(DEFINED ICEBERG_CMAKE_MODULE_PATH_OLD)
    set(CMAKE_MODULE_PATH ${ICEBERG_CMAKE_MODULE_PATH_OLD})
    unset(ICEBERG_CMAKE_MODULE_PATH_OLD)
  else()
    unset(CMAKE_MODULE_PATH)
  endif()
endmacro()

macro(iceberg_find_components components)
  foreach(comp ${components})
    string(TOLOWER "${comp}" _comp_lower_case)
    if(TARGET "iceberg::iceberg_${_comp_lower_case}_shared" OR
       TARGET "iceberg::iceberg_${_comp_lower_case}_static")
      set(iceberg_${comp}_FOUND TRUE)
    else()
      set(iceberg_${comp}_FOUND FALSE)
      set(iceberg_NOT_FOUND_MESSAGE "Component ${comp} was not installed")
    endif()
  endforeach()
endmacro()

# Find system dependencies
iceberg_find_dependencies("${ICEBERG_SYSTEM_DEPENDENCIES}")

# Create missing CRoaring targets if they don't exist
if(NOT TARGET roaring::roaring-headers)
  add_library(roaring::roaring-headers INTERFACE IMPORTED)
endif()

if(NOT TARGET roaring::roaring-headers-cpp)
  add_library(roaring::roaring-headers-cpp INTERFACE IMPORTED)
endif()

if(NOT TARGET CURL::libcurl)
  add_library(CURL::libcurl INTERFACE IMPORTED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/iceberg-targets.cmake")

if(TARGET iceberg::arrow_static)
  add_library(Arrow::arrow_static ALIAS iceberg::arrow_static)

  add_library(Arrow::arrow_bundled_dependencies STATIC IMPORTED)
  get_target_property(arrow_static_configurations iceberg::arrow_static
                      IMPORTED_CONFIGURATIONS)
  foreach(CONFIGURATION ${arrow_static_configurations})
    string(TOUPPER "${CONFIGURATION}" CONFIGURATION)
    get_target_property(arrow_static_location iceberg::arrow_static
                        LOCATION_${CONFIGURATION})
    get_filename_component(arrow_lib_dir "${arrow_static_location}" DIRECTORY)
    set_property(TARGET Arrow::arrow_bundled_dependencies
                 APPEND
                 PROPERTY IMPORTED_CONFIGURATIONS ${CONFIGURATION})
    set_target_properties(Arrow::arrow_bundled_dependencies
                          PROPERTIES IMPORTED_LOCATION_${CONFIGURATION}
                                     "${arrow_lib_dir}/${CMAKE_STATIC_LIBRARY_PREFIX}arrow_bundled_dependencies${CMAKE_STATIC_LIBRARY_SUFFIX}"
    )
  endforeach()
endif()

if(TARGET iceberg::parquet_static)
  add_library(Parquet::parquet_static ALIAS iceberg::parquet_static)
endif()

# Find required components
iceberg_find_components("${iceberg_FIND_COMPONENTS}")

check_required_components(iceberg)
